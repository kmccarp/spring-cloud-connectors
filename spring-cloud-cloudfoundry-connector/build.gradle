description = 'Spring Cloud Connectors Cloud Foundry Connector'

buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
	}
}

apply plugin: 'com.github.johnrengelman.shadow'

ext {
	jacksonVersion = "2.10.0"
}

dependencies {
	compile project(':spring-cloud-connectors-core')
	compile("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
}

shadowJar {
	classifier = null
	dependencies {
		include dependency('com.fasterxml.jackson.core:jackson-databind:.*')
		exclude 'module-info.class',
				'com/fasterxml/jackson/databind/annotation/*',
				'com/fasterxml/jackson/databind/cfg/*',
				'com/fasterxml/jackson/databind/deser/**/*',
				'com/fasterxml/jackson/databind/exc/*',
				'com/fasterxml/jackson/databind/ext/*',
				'com/fasterxml/jackson/databind/introspect/*',
				'com/fasterxml/jackson/databind/json*/**/*',
				'com/fasterxml/jackson/databind/node/*',
				'com/fasterxml/jackson/databind/ser/**/*',
				'com/fasterxml/jackson/databind/type/*',
				'com/fasterxml/jackson/databind/util/*'
	}
	relocate('com.fasterxml.jackson.databind', 'org.springframework.cloud.cloudfoundry.com.fasterxml.jackson.databind')
}

assemble.dependsOn shadowJar
install.dependsOn shadowJar

configurations {
	tests
}

task testJar(type: Jar) {
	classifier = 'tests'
	from sourceSets.test.output
}

build.dependsOn testJar

artifacts {
	tests testJar
	archives testJar
}
